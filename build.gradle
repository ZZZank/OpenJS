buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "dev.architectury.loom" version "1.7-SNAPSHOT" // TODO: WHY I CANNOT USE 1.5 or higher
}

def props = project.properties
def sysProps = System.getProperties()

if (project.hasProperty("run_number")) {
    println("On GitHub runs #${props.run_number}")
}
println("Java: ${ -> sysProps['java.version']} | JVM: ${ -> sysProps['java.vm.version']} | Vendor: ${ -> sysProps['java.vendor']} | Architecture: ${ -> sysProps['os.arch']}")

apply plugin: "dev.architectury.loom"

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

loom {
    silentMojangMappingsLicense()

    forge {
        mixinConfig "${mod_id}.mixins.json"
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

dependencies {
    minecraft "net.minecraft:minecraft:${minecraft_version}"

    mappings(loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_version}")
    })

    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation("org.mozilla:rhino:${rhino_version}")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

processResources {
    var replaceProperties = [minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
                             forge_version       : forge_version, forge_version_range: forge_version_range,
                             loader_version_range: loader_version_range,
                             mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
                             mod_authors         : mod_authors, mod_description: mod_description,]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

shadowJar {
    dependencies {
        include (dependency "org.mozilla:rhino")
    }
    archiveClassifier.set("")
}

remapJar {
    setInput(shadowJar.archiveFile)
    dependsOn(shadowJar)
}

assemble.dependsOn(shadowJar)

jar {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : mod_authors,
                    "Specification-Version"   : "1", // We are version 1 of ourselves
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : mod_authors,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")])
    }

    finalizedBy 'shadowJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation

    options.release = 17
}
